# ------------------------------------------------------------------------------
# Energy Technology Choice Model
# ------------------------------------------------------------------------------
# This partial model implements the technology choice model for energy services
# using a smooth transition approach with log-normal distributions.

# ------------------------------------------------------------------------------
# 1. Variable and Dummy Definitions
# ------------------------------------------------------------------------------
$IF %stage% == "variables":

# 1.1 Dummy Variables
$SetGroup SG_Energy_technology_dummies
  d1sqTPotential[l,es,d,t] "Dummy determining the existence of technology potentials"
  d1pTK[d,t] "Dummy determining the existence of user costs for technologies"
  d1uTE[l,es,e,d,t] "Dummy determining the existence of energy input in technology"
  d1qES_e[es,e,d,t] "Dummy determining the existence of energy use (sum across technologies)"
  d1qES[es,d,t] "Dummy determining the existence of energy service, quantity"
  d1switch_energy_technology[t] "Dummy to control whether the energy technology model is turned on (=1) or off (=0)"
  d1switch_integrate_energy_technology[t] "Dummy to control whether the energy technology model is integrated with the CGE-model (=1) or not (=0)"
;

# 1.2 Main Variables
$Group+ all_variables
  # 1.2.1 Exogenous Variables

  # 1.2.1.1 Exogenous Input Prices
  pTK[d,t]$(d1pTK[d,t]) "User cost of capital in technologies for energy services"
  jpTK[i,t] "Share parameter linking capital user cost in energy technology model to CGE model"

  # 1.2.1.2 Exogenous Energy Service Demand
  qES[es,d,t]$(d1qES[es,d,t]) "Energy service, quantity."
  jES[es,d,t]$(d1qES[es,d,t]) "Share parameter linking energy service in energy technology model to energy service demand in CGE model"
  
  # 1.2.1.3 Exogenous Technology Parameters
  sqTPotential[l,es,d,t]$(d1sqTPotential[l,es,d,t]) "Potential supply by technology l in ratio of energy service (share of qES)"
  theta[l,es,d,t]$(d1sqTPotential[l,es,d,t]) "jsk same sqTPotential. only used adhoc for loading data"
  eP[l,es,d,t]$(d1sqTPotential[l,es,d,t]) "Parameter governing efficiency of costs of technology l (smoothing parameter)"
  uTE[l,es,e,d,t]$(d1uTE[l,es,e,d,t]) "Input of energy in technology l per PJ output at full potential"

  vTI[l,es,d,t]$(d1sqTPotential[l,es,d,t]) "Investment cost, billion EUR per PJ output at full potential"
  vTC[l,es,d,t]$(d1sqTPotential[l,es,d,t]) "Variable capital costs, billion EUR per PJ output at full potential"

  # 1.2.2 Core Endogenous Variables
  # 1.2.2.1 Levelized Cost of Energy (LCOE) 
  uTKexp[l,es,d,t]$(d1sqTPotential[l,es,d,t]) "Average input of machinery capital in technology l per PJ output at full potential"

  # 1.2.2.1 Marginal Capital Intensity
  uTKmargNoBound[l,es,d,t]$(d1sqTPotential[l,es,d,t]) "Input of machinery capital in technology l per PJ output at the margin of supply - Unrestricted"
  uTKmarg[l,es,d,t]$(d1sqTPotential[l,es,d,t]) "Input of machinery capital in technology l per PJ output at the margin of supply - Lower bounded"
  
  # 1.2.2.2 Prices
  pTPotential[l,es,d,t]$(d1sqTPotential[l,es,d,t]) "Average price of technology l at full potential, ie. when sTSupply=sqTPotential"
  pT[l,es,d,t]$(d1sqTPotential[l,es,d,t]) "Average price of technology l at level of supply"
  pESmarg[es,d,t]$(sum(l, d1sqTPotential[l,es,d,t])) "Marginal price of energy services based on the supply by technologies"

  # 1.2.2.3 Supply Variables
  sqT[l,es,d,t]$(d1sqTPotential[l,es,d,t])   "Supply by technology l in ratio of energy service (share of qES)"

  # 1.2.3 Output Variables
  # 1.2.3.1 Values and Prices
  vTSupply[l,es,d,t]$(d1sqTPotential[l,es,d,t]) "Value (or costs) of energy service supplied by technology l "
  vES[es,d,t]$(sum(l, d1sqTPotential[l,es,d,t])) "Value of energy service" 
  pES[es,d,t]$(sum(l, d1sqTPotential[l,es,d,t])) "Energy service, price."
  vESK[es,d,t]$(d1qES[es,d,t]) "Value of machinery capital"

  # 1.2.3.2 Input Quantities
  qESE[es,e,d,t]$(d1qES_e[es,e,d,t]) "Quantity of energy in energy services"
  qESK[es,d,t]$(d1qES[es,d,t]) "Quantity of machinery capital in energy services"

  # Variables for integration with CGE-model
  qESE_baseline[es,e,d,t]$(d1qES_e[es,e,d,t]) "Energy input in the energy technology model (baseline)"
  Delta_qESE[es,e,d,t]$(d1qES_e[es,e,d,t]) "Difference between energy input in the energy technology model (difference between shock and baseline)"
  jqESE[es,e,i,t]$(d1qES_e[es,e,i,t] and d1pREa[es,e,i,t]) "Share parameter linking energy input in the energy technology model to energy input in the CGE-model"
  qESK_baseline[es,d,t]$(d1qES[es,d,t]) "Capital input in the energy technology model (baseline)"
  Delta_qESK[es,d,t]$(d1qES[es,d,t]) "Difference between capital input in the energy technology model (difference between shock and baseline)"
  vESK_baseline[es,d,t]$(d1qES[es,d,t]) "Value of machinery capital (baseline)"
  Delta_vESK[es,d,t]$(d1qES[es,d,t]) "Difference between value of machinery capital in the energy technology model (difference between shock and baseline)"
  qEmmE_CCS[es,e,d,t]$(d1qES_e[es,e,d,t] and sameas(e,'Captured CO2')) "Quantity of CCS in energy services"
;

parameter
  LifeSpan[l,es,d,t] "Life span of technology l in years"
  DiscountRate[l,es,d] "Discount rate of technology l"
  ;

$ENDIF # variables

# ------------------------------------------------------------------------------
# 2. Model Equations
# ------------------------------------------------------------------------------
$IF %stage% == "equations":

$BLOCK energy_technology_LCOE_equations energy_technology_LCOE_endogenous $(t1.val <= t.val and t.val <= tEnd.val and d1switch_energy_technology[t])

  # Levelized cost of energy (LCOE) in technology l per PJ output at full potential
  $(t.val <= tend.val-LifeSpan[l,es,d,t]+1 and d1sqTPotential[l,es,d,t]).. 
    uTKexp[l,es,d,t] =E=
     (vTI[l,es,d,t] # Investment costs
      + @Discount2t(vTC[l,es,d,tt], DiscountRate[l,es,d], LifeSpan[l,es,d,t], d1sqTPotential[l,es,d,tt])) # Discounted variable costs
        / @Discount2t(1, DiscountRate[l,es,d], LifeSpan[l,es,d,t], d1sqTPotential[l,es,d,tt]) # Dicounted denominator
        ;

  # Levelized cost of energy (LCOE) in technology l per PJ output at full potential
  uTKexp&_tEnd[l,es,d,t]$(t.val > tend.val-LifeSpan[l,es,d,t]+1 and d1sqTPotential[l,es,d,t]).. 
    uTKexp[l,es,d,t] =E= 
     (vTI[l,es,d,t] # Investment costs
      + @Discount2t(vTC[l,es,d,tt], DiscountRate[l,es,d], LifeSpan[l,es,d,t], d1sqTPotential[l,es,d,tt]) # Discounted variable costs until tEnd
      + (1/(1+DiscountRate[l,es,d]))**(1+tEnd.val-t.val)*@FiniteGeometricSeries({vTC[l,es,d,tEnd]}, {DiscountRate[l,es,d]}, {LifeSpan[l,es,d,t]-1+t.val-tEnd.val})) # Discounted variable costs after tEnd (Assuming constant costs after tEnd)
      / (@Discount2t(1, DiscountRate[l,es,d], LifeSpan[l,es,d,t], d1sqTPotential[l,es,d,tt]) # Discount denominator until tEnd
       + (1/(1+DiscountRate[l,es,d]))**(1+tEnd.val-t.val)*@FiniteGeometricSeries({1}, {DiscountRate[l,es,d]}, {LifeSpan[l,es,d,t]-1+t.val-tEnd.val})) # Discounted denominator after tEnd
       ; 

$ENDBLOCK


# 2.1 Core Model Equations
$BLOCK energy_technology_equations_core energy_technology_endogenous_core $(t1.val <= t.val and t.val <= tEnd.val and d1switch_energy_technology[t]) 

  # 2.1.2 Technology Choice Equations
  # Equality between marginal price of energy service and marginal price of technology 
  # determines capital intensity of technologies at the margin of supply
  uTKmargNoBound[l,es,d,t].. pESmarg[es,d,t] =E= 
    sum(e$(d1pEpj[es,e,d,t] and d1uTE[l,es,e,d,t]), uTE[l,es,e,d,t]*pEpj_marg[es,e,d,t]) + 
    uTKmargNoBound[l,es,d,t]*pTK[d,t];

  # A lower bound close to zero is set to avoid indeterminancy in cdfLognorm in sqTqES
  # uTKmargNoBound<=0 happen when a technology is very energy intensive (ineffecient)
  .. uTKmarg[l,es,d,t] =E= 
    @InInterval(0.001, uTKmargNoBound[l,es,d,t], uTKexp[l,es,d,t]*[1+5*eP[l,es,d,t]]);

  # Supply of technology l in ratio of energy service demand qES
  .. sqT[l,es,d,t] =E= 
    sqTPotential[l,es,d,t]*@cdfLogNorm(uTKmarg[l,es,d,t], uTKexp[l,es,d,t], eP[l,es,d,t]);

  # Shadow value identifying marginal technology for energy purpose
  pESmarg[es,d,t].. 1 =E= sum(l$(d1sqTPotential[l,es,d,t]), sqT[l,es,d,t]);
                
$ENDBLOCK

# 2.2 Output Equations
$BLOCK energy_technology_equations_output energy_technology_endogenous_output $(t1.val <= t.val and t.val <= tEnd.val and d1switch_energy_technology[t]) 
  # 2.2.1 Energy and Capital Use
  # Use of energy in production of energy service
  .. qESE[es,e,d,t] =E= 
    qES[es,d,t] * sum(l$(d1sqTPotential[l,es,d,t]), sqT[l,es,d,t] * uTE[l,es,e,d,t]);
    
  # Use of machinery capital for technologies
  .. qESK[es,d,t] =E= 
    qES[es,d,t] * 
    sum(l$(d1sqTPotential[l,es,d,t]),
        sqTPotential[l,es,d,t]*@PartExpLogNorm(uTKmarg[l,es,d,t], uTKexp[l,es,d,t], eP[l,es,d,t]));

  # 2.2.2 Value and Price Calculations
  # Value of energy service                                       
  .. vES[es,d,t] =E= 
    sum(e, qESE[es,e,d,t]*pEpj_marg[es,e,d,t]) + qESK[es,d,t]*pTK[d,t];

  # Price of energy service
  .. pES[es,d,t] =E= vES[es,d,t] / qES[es,d,t];   

  # Value of machinery capital
  .. vESK[es,d,t] =E= qESK[es,d,t]*pTK[d,t];

$ENDBLOCK

$BLOCK energy_technology_equations_links energy_technology_endogenous_links $(t1.val <= t.val and t.val <= tEnd.val and d1switch_energy_technology[t] and d1switch_integrate_energy_technology[t])

  # qES is determined by the CGE-model. jES (exogenous) is the difference between qES and qREes in the baseline
  .. qES[es,i,t] =E= jES[es,i,t]*qREes[es,i,t];

  # pTK is determined by the CGE-model. jpTK (exogenous) is the relative difference between pTK and pK_k_i['iM',i,t] in the baseline
  .. pTK[i,t] =E= pK_k_i['iM',i,t]*jpTK[i,t];

  # Lets see if we need this
  .. Delta_qESE[es,e,d,t] =E= qESE[es,e,d,t] - qESE_baseline[es,e,d,t];

  #jqESE is endogenous when calibrating the model. In shocks, jqESE is exogenous and uREa is endogenous
  jqESE[es,e,i,t].. qESE[es,e,i,t] + jqESE[es,e,i,t] =E= qREa[es,e,i,t];

  # Difference in capital use between the baseline and the shock
  .. Delta_qESK[es,d,t] =E= qESK[es,d,t] - qESK_baseline[es,d,t];

  # Difference in value of capital use between the baseline and the shock
  .. Delta_vESK[es,d,t] =E= vESK[es,d,t] - vESK_baseline[es,d,t];

  # Link energy technology capital investments to factor demand module (aggregate approximation)
  # This endogenizes the J-term defined in factor_demand.gms, aggregating energy technology capital investments
  jDelta_qESK[k,i,t]$(sameas[k,'iM'] and d1K_k_i[k,i,t])..
    jDelta_qESK[k,i,t] =E= sum(es$(d1qES[es,i,t]), Delta_qESK[es,i,t]);

  # Link energy technology capital value differences to production module (aggregate approximation)
  # J-term stands in for sum(es, Delta_vESK[es,i,t]) used in production.gms equation
  jDelta_vESK[i,t]$(d1Y_i[i,t])..
    jDelta_vESK[i,t] =E= sum(es$(d1qES[es,i,t]), Delta_vESK[es,i,t]);
    
  # CCS in energy services
  .. qEmmE_CCS[es,e,d,t] =E= qESE[es,e,d,t];

$ENDBLOCK

# 2.3 Model Assembly
$GROUP energy_technology_endogenous
  energy_technology_LCOE_endogenous
  energy_technology_endogenous_core
  energy_technology_endogenous_output
  energy_technology_endogenous_links
;

$MODEL energy_technology_equations  
  energy_technology_LCOE_equations
  energy_technology_equations_core
  energy_technology_equations_output 
  energy_technology_equations_links
;

# Add equation and endogenous variables to main model
model main / energy_technology_equations /;
$GROUP+ main_endogenous energy_technology_endogenous;

# Create partial energy technology model
$GROUP energy_technology_partial_endogenous
  energy_technology_LCOE_endogenous
  energy_technology_endogenous_core
  energy_technology_endogenous_output
;

$MODEL energy_technology_partial_equations  
  energy_technology_LCOE_equations
  energy_technology_equations_core
  energy_technology_equations_output 
;

# 2.4 Solver Helper Function
$FUNCTION Setbounds_energy_technology():
  # Set bounds for uTKmarg
  uTKmarg.lo[l,es,d,t]$(sqTPotential.l[l,es,d,t] and uTKmarg.lo[l,es,d,t] ne uTKmarg.up[l,es,d,t]) = 0.001*0.99;
  uTKmarg.up[l,es,d,t]$(sqTPotential.l[l,es,d,t] and uTKmarg.lo[l,es,d,t] ne uTKmarg.up[l,es,d,t]) = 
    uTKexp.l[l,es,d,t]*[1+5*eP.l[l,es,d,t]]*1.01;
  
  # Set bounds for sqT
  sqT.lo[l,es,d,t]$(sqTPotential.l[l,es,d,t] and sqT.lo[l,es,d,t] ne sqT.up[l,es,d,t]) = 0.00000000001;
  sqT.up[l,es,d,t]$(sqTPotential.l[l,es,d,t] and sqT.lo[l,es,d,t] ne sqT.up[l,es,d,t]) = sqTPotential.l[l,es,d,t];
  
  # Set bounds for pESmarg
  pESmarg.lo[es,d,t]$(sum(l,sqTPotential.l[l,es,d,t]) and pESmarg.lo[es,d,t] ne pESmarg.up[es,d,t]) = 0.001;
$ENDFUNCTION

$ENDIF # equations

# ------------------------------------------------------------------------------
# 3. Data and Parameters
# ------------------------------------------------------------------------------
$IF %stage% == "exogenous_values":

# 3.1 Data Loading
$GROUP energy_technology_data_variables
  sqTPotential[l,es,d,t]
  uTE[l,es,e,d,t]
  vTI[l,es,d,t]
  vTC[l,es,d,t]
  pTK[d,t]
  qES[es,d,t]

;

# Load data from generic dummy data
$IF1 %generic_energy_technology_data% == 1:
  @load(energy_technology_data_variables, "../data/data.gdx")
  $GROUP+ data_covered_variables energy_technology_data_variables;

  # Load LifeSpan from data.gdx
  execute_load "../data/data.gdx" LifeSpan=LifeSpan;
$ENDIF1

# Load data from excel-based data
$IF1 %generic_energy_technology_data% == 0:
  @load(energy_technology_data_variables, "../data/Energy_technology_data/Excel_data/Energy_technology_data.gdx")
  $GROUP+ data_covered_variables energy_technology_data_variables;

  # Load LifeSpan from data.gdx
  execute_load "../data/Energy_technology_data/Excel_data/Energy_technology_data.gdx" LifeSpan=LifeSpan;
$ENDIF1

# 3.2 Initial Values
# Set discount rate
DiscountRate[l,es,d]$(sum(t, sqTPotential.l[l,es,d,t])) = 0.05;

# Set smoothing parameters
eP.l[l,es,d,t]$(sqTPotential.l[l,es,d,t]) = 0.03;
eP.l[l,es,'55560',t]$(sqTPotential.l[l,es,'55560',t]) = 0.05;

# Set share parameter
jES.l[es,i,t]$(qES.l[es,i,t] and qREes.l[es,i,t]) = qES.l[es,i,t]/qREes.l[es,i,t];
jpTK.l[i,t]$(d1pTK[i,t] and d1K_k_i['iM',i,t]) = pTK.l[i,t]/pK_k_i.l['iM',i,t];

# 3.3 Dummy Variable Setup
# Set dummy determining the existence of technology potentials
d1sqTPotential[l,es,d,t] = yes$(sqTPotential.l[l,es,d,t]);
d1uTE[l,es,e,d,t] = yes$(uTE.l[l,es,e,d,t]);
d1pTK[d,t] = yes$(sum((l,es), d1sqTPotential[l,es,d,t]));
d1qES_e[es,e,d,t] = yes$(sum(l, d1uTE[l,es,e,d,t]));
d1qES[es,d,t] = yes$(qES.l[es,d,t]);

# 4.4 Starting values for Levelized Cost of Energy (LCOE)
uTKexp.l[l,es,d,t]$(t.val <= tend.val-LifeSpan[l,es,d,t]+1 and d1sqTPotential[l,es,d,t]) =
   (vTI.l[l,es,d,t] # Investment costs
    + @Discount2t(vTC.l[l,es,d,tt], DiscountRate[l,es,d], LifeSpan[l,es,d,t], d1sqTPotential[l,es,d,tt])) # Discounted variable costs
      / @Discount2t(1, DiscountRate[l,es,d], LifeSpan[l,es,d,t], d1sqTPotential[l,es,d,tt]) # Dicounted denominator
      ;

  # Levelized cost of energy (LCOE) in technology l per PJ output at full potential
  uTKexp.l[l,es,d,t]$(t.val > tend.val-LifeSpan[l,es,d,t]+1 and d1sqTPotential[l,es,d,t]) =
     (vTI.l[l,es,d,t] # Investment costs
      + @Discount2t(vTC.l[l,es,d,tt], DiscountRate[l,es,d], LifeSpan[l,es,d,t], d1sqTPotential[l,es,d,tt]) # Discounted variable costs until tEnd
      + (1/(1+DiscountRate[l,es,d]))**(1+tEnd.val-t.val)*@FiniteGeometricSeries({vTC.l[l,es,d,tEnd]}, {DiscountRate[l,es,d]}, {LifeSpan[l,es,d,t]-1+t.val-tEnd.val})) # Discounted variable costs after tEnd (Assuming constant costs after tEnd)
      / (@Discount2t(1, DiscountRate[l,es,d], LifeSpan[l,es,d,t], d1sqTPotential[l,es,d,tt]) # Discount denominator until tEnd
       + (1/(1+DiscountRate[l,es,d]))**(1+tEnd.val-t.val)*@FiniteGeometricSeries({1}, {DiscountRate[l,es,d]}, {LifeSpan[l,es,d,t]-1+t.val-tEnd.val})) # Discounted denominator after tEnd
       ; 

pTPotential.l[l,es,d,t] = 
  sum(e, uTE.l[l,es,e,d,t]*pEpj_marg.l[es,e,d,t]) + uTKexp.l[l,es,d,t]*pTK.l[d,t];


$ENDIF # exogenous_values

# ------------------------------------------------------------------------------
# Starting values
# ------------------------------------------------------------------------------
$IF %stage% == "starting_values":

set_time_periods(%calibration_year%, %calibration_year%);

$Group non_default_starting_values
  # Variables that require custom starting values
;

# Set custom starting values for the variables in non_default_starting_values here

$ENDIF # starting_values

# ------------------------------------------------------------------------------
# 4. Calibration
# ------------------------------------------------------------------------------
$IF %stage% == "calibration":


model calibration / energy_technology_equations /;

# 4.2 Calibration Variables
$GROUP calibration_endogenous
  energy_technology_endogenous
  calibration_endogenous
;

# 4.3 Flat Variables After Last Data Year
$Group+ G_flat_after_last_data_year
  vES[es,d,t]
  qESE[es,e,d,t]
  qESK[d,t]
  vTSupply[l,es,d,t]
  uTKmarg[l,es,d,t]
  pESmarg[es,d,t]
  sqT[l,es,d,t]
  eP[l,es,d,t]
;


$ENDIF # calibration